plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
}

group 'de.akquinet'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task computeAverageIncomeKotlin(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    main = "ComputeAverageIncomeKt"
}

// Stack must be installed and the executable must be in the path
// https://docs.haskellstack.org/en/stable/README/

task cleanAverageIncomeHaskell(type: Exec) {
    workingDir "./src/main/Haskell/compute-average-income"
    commandLine "stack", "clean"
}

task buildForProfileAverageIncomeHaskell(type: Exec) {
    workingDir "./src/main/Haskell/compute-average-income"
    commandLine "stack", "build" , "--profile"
}
task buildAverageIncomeHaskell(type: Exec) {
    workingDir "./src/main/Haskell/compute-average-income"
    commandLine "stack", "build"
}

task computeAverageIncomeHaskell(type: Exec) {
    dependsOn(cleanAverageIncomeHaskell)
    dependsOn(buildAverageIncomeHaskell)
    buildAverageIncomeHaskell.mustRunAfter(cleanAverageIncomeHaskell)

    workingDir "./src/main/Haskell/compute-average-income"
    commandLine "stack", "exec", "compute-average-income-exe" , "+RTS" , "-N6"
}

task profileAverageIncomeHaskell(type: Exec) {
    dependsOn(cleanAverageIncomeHaskell)
    dependsOn(buildForProfileAverageIncomeHaskell)
    buildForProfileAverageIncomeHaskell.mustRunAfter(cleanAverageIncomeHaskell)

    workingDir "./src/main/Haskell/compute-average-income"
    commandLine "stack", "exec" , "--profile", "--", "compute-average-income-profile-exe", "+RTS" , "-s" // simple statistics
    //commandLine "stack", "exec" , "--profile", "--", "compute-average-income-profile-exe", "+RTS" , "-P", "-S" // extensive profile
    //commandLine "stack", "exec" , "--profile", "--", "compute-average-income-exe", "+RTS" , "-P", "-s"
}


// Rust must be installed using rustup
// https://www.rust-lang.org
    task computeAverageIncomeRust(type: Exec) {
    workingDir "./src/main/Rust/compute_average_income"
    commandLine "cargo", "run", "--release", "--package", "compute_average_income",  "--bin", "compute_average_income"
}

task computeAverageIncome {
    dependsOn "computeAverageIncomeRust"
    dependsOn "computeAverageIncomeHaskell"
    dependsOn "computeAverageIncomeKotlin"
}
